dnl Process this file with autoconf to produce a configure script.

dnl Schism Tracker - a cross-platform Impulse Tracker clone
dnl copyright (c) 2003-2005 chisel <schism@chisel.cjb.net>
dnl URL: http://rigelseven.com/schism/
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA


AC_INIT

if test -d schism; then
	echo "You cannot ./configure from here, try something like this:" >&2
	echo "	mkdir build && cd build && ../configure && make" >&2
	exit 1
fi

AC_CONFIG_SRCDIR([schism/main.c])

dnl We'll need machine type later
AC_CANONICAL_TARGET([])
machtype="$target_cpu"

dnl when changing the version, also change the CWTV in audio_loadsave.cc
dnl (TODO: come up with some way to do it automatically)
AM_INIT_AUTOMAKE(schism, 1.0)
AM_CONFIG_HEADER(config.h)

dnl -----------------------------------------------------------------------

dnl Check for standard programs
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB

dnl enable win32 dll libtool support
AC_LIBTOOL_WIN32_DLL

dnl We're using C
AC_LANG([C])

dnl check endianness
AC_C_BIGENDIAN

dnl Check for SDL libs
AM_PATH_SDL(1.1.8, , AC_MSG_ERROR([*** SDL version >= 1.1.8 not found.]))

enable_sdlstatic=no
AC_ARG_ENABLE(static-sdl, [  --enable-static-sdl       Link SDL statically], enable_sdlstatic=yes,enable_sdlstatic=no)
if test "x$enable_sdlstatic" = "xyes"; then
	if test "x$SDL_CONFIG" = "xno"; then
		echo "*** SDL_CONFIG explicitly set to 'no' but you asked for static-sdl"
		echo "*** one of those has to go..."
		exit 1
	else
		SDL_LIBS=`$SDL_CONFIG $sdlconf_args --static-libs`
		AC_SUBST(SDL_LIBS)
	fi
fi

dnl CoreMIDI (MacosX)
AC_MSG_CHECKING(for CoreAudio/CoreMIDI Framework)
if echo "$SDL_LIBS" | grep -- -framework >/dev/null 2>&1; then
	AC_MSG_RESULT(found)
	SDL_LIBS="$SDL_LIBS -framework CoreAudio -framework CoreMIDI -framework IOKit -framework OpenGL"
	AC_SUBST(SDL_LIBS)

	AM_CONDITIONAL([USE_MACOSX], true)
	AM_CONDITIONAL([am__fastdepOBJC], true)
else
	AC_MSG_RESULT(not found)
	AM_CONDITIONAL([USE_MACOSX], false)
	AM_CONDITIONAL([am__fastdepOBJC], false)
fi

dnl Functions
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(strchr memmove strerror strtol strcasecmp strncasecmp strverscmp stricmp strnicmp asprintf vasprintf realpath memcmp mmap socket)
AM_CONDITIONAL([NEED_GNU_ASPRINTF], [test "$ac_cv_func_asprintf" = "no"])
AM_CONDITIONAL([NEED_GNU_VASPRINTF], [test "$ac_cv_func_vasprintf" = "no"])
AM_CONDITIONAL([NEED_GNU_REALPATH], [test "$ac_cv_func_realpath" = "no"])
AM_CONDITIONAL([NEED_GNU_MEMCMP], [test "$ac_cv_func_memcmp" = "no"])
AM_CONDITIONAL([USE_MMAP], [test "$ac_cv_func_mmap" = "yes"])

dnl Headers, typedef crap, et al.
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_HEADER_TIME
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h sys/ioctl.h sys/kd.h linux/fb.h byteswap.h sys/soundcard.h X11/Xlib.h X11/extensions/XKB.h)
AM_CONDITIONAL([USE_OSS], [test "$ac_cv_header_sys_soundcard_h" = yes])

AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_STRUCT_TM
AC_CHECK_LIBM
AC_SUBST(LIBM)

dnl -----------------------------------------------------------------------

saved_libs=$LIBS
alsa=no
AC_CHECK_LIB(asound, snd_seq_open,[alsa=yes])
alsadltrick=no
if test "$alsa" = "yes"
then if test "$ac_cv_header_sys_soundcard_h" = "yes"
then alsadltrick=yes
LIBS=$saved_libs
fi
fi
AM_CONDITIONAL([USE_ALSA], [test "$alsa" = yes])
AM_CONDITIONAL([USE_ALSA_DLTRICK], [test "$alsadltrick" = yes])

x11=no
AC_CHECK_LIB(X11, XOpenDisplay,[x11=yes])
if test "$ac_cv_header_X11_Xlib_h" = "yes"
then x11="yes"
fi
AC_CHECK_LIB(Xxf86misc, XF86MiscGetKbdSettings)
AM_CONDITIONAL([USE_X11], [test "$x11" = yes])
AM_CONDITIONAL([USE_ALSA], [test "$alsa" = yes])
if test "$ac_cv_lib_Xxf86misc_XF86MiscGetKbdSettings" = "yes"
then 
	AC_CHECK_HEADERS(X11/extensions/xf86misc.h)
	if test "$ac_cv_header_X11_extensions_xf86misc_h" = "yes"
	then 
		SDL_LIBS="$SDL_LIBS -lXxf86misc"
		AC_SUBST(SDL_LIBS)
	fi
fi
AC_ARG_WITH(windres,
	[  --with-windres=RSC      Name of windres tool (optional)],
	windres="$withval", windres="")
AM_CONDITIONAL([USE_WINDRES], [test "x$windres" != "x"])

WINDRES="$windres"
AC_SUBST(WINDRES)

dnl winmm testing...
AC_CHECK_HEADERS(winsock.h winsock2.h windows.h)
if test "X$ac_cv_header_windows_h" = "Xyes"
then
	AM_CONDITIONAL([USE_WIN32MM], true)
	SDL_LIBS="$SDL_LIBS -lwinmm"
	AC_SUBST(SDL_LIBS)
else
	AM_CONDITIONAL([USE_WIN32MM], false)
fi

if test "X$ac_cv_func_socket" = "Xyes"
then
	dnl free networking
	AM_CONDITIONAL([USE_NETWORK], [test "x" = "x"])
else
	AC_CHECK_HEADERS(winsock.h winsock2.h sys/socket.h)
	if test "x$ac_cv_header_winsock_h" = "xyes"
	then	AM_CONDITIONAL([USE_NETWORK], true)
		SDL_LIBS="$SDL_LIBS -lwsock32"
		AC_SUBST(SDL_LIBS)
	else if test "x$ac_cv_header_winsock2_h" = "xyes"
		then	AM_CONDITIONAL([USE_NETWORK], true)
			SDL_LIBS="$SDL_LIBS -lws2_32"
			AC_SUBST(SDL_LIBS)
		else if test "x$ac_cv_header_sys_socket_h" = "xyes"
			then	AM_CONDITIONAL([USE_NETWORK], true)
				SDL_LIBS="$SDL_LIBS -lsocket"
				AC_SUBST(SDL_LIBS)
			fi
		fi
	fi
fi

#AC_CHECK_LIB(kernel32, GetConsoleMode, SDL_LIBS="$SDL_LIBS -Wl,--subsystem,console")

dnl wee...
dnl this completely sucks...
OBJC=$CC
CFLAGS=$CFLAGS
AC_SUBST(OBJC)
AC_SUBST(OBJCFLAGS)

dnl -----------------------------------------------------------------------
dnl Optimizations borrowed and modified a bit from DGen.
dnl I really don't know what they all do, to be honest :)
dnl (This ought to be above AC_PROG_CC, but that causes configure to fail
dnl when all the insane warnings are are enabled.)

AC_ARG_ENABLE(extra-opt,
	      AS_HELP_STRING([--enable-extra-opt], [Add extra optimizations (egcs/GCC >= 2.95 only)]),
              ADD_OPT=$enableval,
              ADD_OPT=no)

AC_ARG_ENABLE(all-warnings,
	      AS_HELP_STRING([--enable-all-warnings], [Enable ridiculous compiler warnings (GCC)]),
              ADD_WARN=$enableval,
              ADD_WARN=no)

if test x$ADD_OPT \!= xno; then
	ADD_OPT="-g0 -s -O3 -ffast-math -fomit-frame-pointer -fno-exceptions"
	ADD_OPT="$ADD_OPT -funroll-loops -frerun-cse-after-loop -fno-ident"
	ADD_OPT="$ADD_OPT -fno-strength-reduce"
	CFLAGS="$CFLAGS $ADD_OPT"
	CXXFLAGS="$CXXFLAGS $ADD_OPT -fno-rtti -fno-enforce-eh-specs"
fi

if test x$ADD_WARN \!= xno; then
        ADD_WARN="-Wall -W -Winline -Wshadow -Wcast-align -Wwrite-strings"
        ADD_WARN="$ADD_WARN -Waggregate-return -Wpacked"
        CFLAGS="$CFLAGS $ADD_WARN -Wstrict-prototypes -Wmissing-prototypes"
        CFLAGS="$CFLAGS -Wmissing-declarations -Wnested-externs"
        CXXFLAGS="$CXXFLAGS $ADD_WARN"
fi


dnl - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
